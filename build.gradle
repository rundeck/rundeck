/**
 * Rundeck Master-Build Project
 * 
 * This project does not contain any buildable code and does not generate any 
 * artifacts, however it allows us to set defaults for the subjects and provides 
 * Groovy syntax highlighting for gradle build files (like this one)
 **/

description = 'The master-build project for Rundeck';

apply plugin: 'groovy';
apply plugin: 'eclipse';
apply plugin: 'idea'

eclipse.project.name = 'rundeck'

/**
 * Defaults for all projects
 */
subprojects {

    // set the eclipse project naming convention to rundeck:<path>:<projectName>
    // so it matches the logical hierarchy more closely
    apply plugin: 'eclipse'
    eclipse.project.name = "${project.getParent().eclipse.project.name}:${name}"
    
    // the environment can be set by adding -Penvironment={value} as a command
    // line switch. by default we run as 'development'
    ext.environment = project.hasProperty('environment') ? environment : 'development';
    
    // Unless we're doing a release build, append -SNAPSHOT to the end of the
    // artifacts. Otherwise use the currentVersion as defined in gradle.properties
    version = project.hasProperty('environment') && environment == 'release' ? 
        currentVersion : currentVersion + '-SNAPSHOT';
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.2'
}
