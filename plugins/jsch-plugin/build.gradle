plugins {
    id 'groovy'
}

defaultTasks 'clean','build'
apply plugin: 'java'
apply plugin: 'groovy'

ext.pluginClassNames='org.rundeck.plugins.jsch.JschNodeExecutor,org.rundeck.plugins.jsch.JschScpFileCopier'
ext.pluginName = 'Script Node Step Plugin'
ext.pluginName = 'JSCH Plugin'
ext.pluginDescription ='SSH Node Executor and File Copier plugin based on JSCH library.'

configurations{
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation {
        extendsFrom pluginLibs
    }
}

dependencies {
    implementation(project(":core"))
    pluginLibs('com.kohlschutter.junixsocket:junixsocket-core:2.10.1')
    pluginLibs("com.github.mwiede:jsch:${mwiedeJschVersion}")
    pluginLibs('org.apache.ant:ant-jsch:1.10.14'){
        //ant is part of core
        exclude(group: 'org.apache.ant', module: 'ant')
        exclude(group: 'com.jcraft', module: 'jsch')
    }

    testImplementation 'junit:junit:4.13.2'
    
    // Spock + Mockito mocking dependencies (Pattern #10 & #74)
    testImplementation "org.mockito:mockito-core:${mockitoInlineVersion}"
    testImplementation "net.bytebuddy:byte-buddy:${byteBuddyVersion}"
    testImplementation "org.objenesis:objenesis:${objenesisVersion}"
    testImplementation "cglib:cglib-nodep:${cglibNoDepVersion}"
    
    testImplementation "org.apache.groovy:groovy-all:${groovyVersion}"
    testImplementation platform("org.spockframework:spock-bom:${spockVersion}")
    testImplementation "org.spockframework:spock-core"
    testImplementation "org.spockframework:spock-junit4"
    
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'

}

repositories {
    mavenLocal()
    mavenCentral()
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

tasks.withType(Test) {
    useJUnitPlatform()
    jvmArgs(
        '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
        '--add-opens', 'java.base/java.util=ALL-UNNAMED'
    )
}



jar {
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-Name': pluginName
        attributes 'Rundeck-Plugin-Description': pluginDescription
        attributes 'Rundeck-Plugin-Tags': 'java,executor'
        def libList = configurations.pluginLibs.collect { 'lib/' + it.name }.join(' ')
        attributes 'Rundeck-Plugin-Libs': "${libList}"
        attributes 'Rundeck-Plugin-Libs-Load-First': "false"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

