/*
 * Copyright 2016 SimplifyOps, Inc. (http://simplifyops.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext.pluginClassNames='org.rundeck.plugin.scm.git.GitExportPluginFactory,org.rundeck.plugin.scm.git.GitImportPluginFactory'
ext.pluginName = 'Git Plugin'
ext.pluginDescription = 'Export and Import Jobs with a Git repository'

apply plugin: "groovy"

apply from: "../../gradle/java-version.gradle"

configurations{
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation {
        extendsFrom pluginLibs
    }
}
repositories {
    mavenLocal()
    maven{ //snapshot dependency
        url='https://oss.sonatype.org/content/repositories/snapshots/'
    }
    mavenCentral()
}

dependencies {
    implementation project(":core")
    pluginLibs( "org.eclipse.jgit:org.eclipse.jgit:5.13.1.202206130422-r") {
        exclude module: 'slf4j-api'
        exclude group: 'org.bouncycastle'
    }
    pluginLibs ("org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:5.13.1.202206130422-r"){
        exclude module: 'slf4j-api'
        exclude group: 'org.bouncycastle'
    }
    implementation 'org.slf4j:slf4j-api:1.7.29'
    implementation "org.codehaus.groovy:groovy:${groovyVersion}"
    implementation group: 'com.hierynomus', name: 'sshj', version: '0.33.0'
    implementation group: 'com.hierynomus', name: 'asn-one', version: '0.6.0'
    implementation 'net.i2p.crypto:eddsa:0.3.0'

    pluginLibs group: 'com.hierynomus', name: 'sshj', version: '0.33.0', ext: 'jar'
    pluginLibs group: 'com.hierynomus', name: 'asn-one', version: '0.6.0', ext: 'jar'
    pluginLibs 'net.i2p.crypto:eddsa:0.3.0'

    implementation ("org.bouncycastle:bcprov-jdk15on:${bouncyCastleVersion}")
    implementation ("org.bouncycastle:bcpg-jdk15on:${bouncyCastleVersion}")
    testImplementation 'junit:junit:4.8.1',
                'org.mockito:mockito-all:1.10.19'

    testImplementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
    testImplementation "cglib:cglib-nodep:2.2.2"
    testImplementation "org.spockframework:spock-core:${spockVersion}"

    testImplementation( 'org.eclipse.jgit:org.eclipse.jgit:5.6.0.201912101111-r')
}


// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-Name': pluginName
        attributes 'Rundeck-Plugin-Description': pluginDescription
        def libList = configurations.pluginLibs.collect { 'lib/' + it.name }.join(' ')
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

tasks.withType(Test) {
    useJUnitPlatform()
}
