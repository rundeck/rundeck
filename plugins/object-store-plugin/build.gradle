plugins {
    id 'groovy'
}

apply from: "../../gradle/java-version.gradle"

ext.pluginClassNames='org.rundeck.plugin.objectstore.ObjectStorePlugin'
ext.pluginName = 'Object Store Plugin'
ext.pluginDescription = 'Stores data in an Amazon S3 compliant object store'

configurations{
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    implementation {
        extendsFrom pluginLibs
    }
}

configurations.all {
    resolutionStrategy {
        // Force safe commons-compress versions to fix CVE-2024-25710
        // Only force artifacts that exist for version 1.21
        force "org.apache.commons:commons-compress:${commonsCompressVersion}"
        // Let Spring Boot BOM manage commons-lang3 version
    }
}


dependencies {
    // Use the latest Groovy version for building this library
    implementation "org.apache.groovy:groovy:${groovyVersion}"
    pluginLibs("io.minio:minio:${minioVersion}") {
        exclude(group: 'com.fasterxml.jackson.core')
        exclude(group: 'com.google.guava', module: 'guava')
        exclude(group: 'commons-io', module: 'commons-io')
        exclude(group: 'com.squareup.okhttp3', module: 'okhttp')
    }
    // Force compatible OkHttp 4.x version for Kotlin 1.3.70 compatibility
    pluginLibs("com.squareup.okhttp3:okhttp:${okhttpVersion}")

    implementation "org.apache.commons:commons-compress:${commonsCompressVersion}"

    // Use the awesome Spock testing and specification framework
    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
    testImplementation "org.testcontainers:testcontainers:${testContainersVersion}"
}

repositories {
    mavenLocal()
    mavenCentral()
}

task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-Name': pluginName
        attributes 'Rundeck-Plugin-Description': pluginDescription
        def libList = configurations.pluginLibs.collect { 'lib/' + it.name }.join(' ')
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)
tasks.withType(Test) {
    useJUnitPlatform()
    jvmArgs += [
        '--add-opens=java.base/java.lang=ALL-UNNAMED',
        '--add-opens=java.base/java.lang.reflect=ALL-UNNAMED',
        '--add-opens=java.base/java.util=ALL-UNNAMED'
    ]
}