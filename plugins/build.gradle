import java.text.SimpleDateFormat

/*
 * Copyright 2016 SimplifyOps, Inc. (http://simplifyops.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * The Rundeck plugins parent project
 */

eclipse.project.name =  "${project.getParent().eclipse.project.name}:plugins";

ext.rundeckPluginVersion= '1.2'
subprojects{

    apply from: "${projectDir}/../../gradle/java.gradle"

    defaultTasks 'clean','build'
    archivesBaseName = "rundeck-$project.name"

    dependencies {
        compile project(":core")
    }
    
    jar {
        doFirst {
            manifest {
                attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 
                        'Rundeck-Plugin-Archive': 'true', 
                        'Rundeck-Plugin-File-Version': version,
                        'Rundeck-Plugin-Author': 'Rundeck, Inc.',
                        'Rundeck-Plugin-URL': 'http://rundeck.com',
                        'Rundeck-Plugin-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssX")
            }
        }
    }
}
apply plugin: 'maven';


task('createPom').doLast {
    pom {
        project {
            artifactId 'rundeck-bundled-plugins'
            groupId project.group
            inceptionYear '2011'
            packaging 'pom'
            version version
            name "RunDeck Bundled Plugins"
            url 'http://rundeck.org'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            modules{
                module 'stub-plugin'
                module 'script-plugin'
                module 'localexec-plugin'
                module 'copyfile-plugin'
            }
            dependencies{
                dependency{
                    artifactId 'rundeck-core'
                    groupId project.group
                    version version
                }
            }
            build{
                plugins{
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-jar-plugin'
                        version '2.3.2'
                        configuration{
                            archive{
                              manifestEntries{
                                'Rundeck-Plugin-Version'(rundeckPluginVersion)
                                'Rundeck-Plugin-Archive'('true')
                                'Rundeck-Plugin-File-Version'(version)
                              }
                            }
                        }
                    }

                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-compiler-plugin'
                        version '2.3.2'
                        configuration{
                            'source'('1.7')
                            'target'('1.7')
                        }
                    }
                }
            }
        }
    }.writeTo("pom.xml")
}

