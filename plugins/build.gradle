/**
 * The Rundeck plugins parent project
 */


eclipse.project.name =  "${project.getParent().eclipse.project.name}:plugins";

ext.rundeckPluginVersion= '1.1'
subprojects{


    defaultTasks 'clean','build'
    archivesBaseName = "rundeck-$project.name"

    dependencies {
        compile project(":core")
    }
    
    jar {
        manifest {
            attributes 'Rundeck-Plugin-Version': rundeckPluginVersion, 
                       'Rundeck-Plugin-Archive': 'true', 
                       'Rundeck-Plugin-File-Version': version,
                       'Rundeck-Plugin-Author': 'SimplifyOps, Inc.',
                       'Rundeck-Plugin-URL': 'http://rundeck.org',
                       'Rundeck-Plugin-BuildDate': new Date().toString()
        }
    }
}
apply plugin: 'maven';


task createPom << {
    pom {
        project {
            artifactId 'rundeck-bundled-plugins'
            groupId project.group
            inceptionYear '2011'
            packaging 'pom'
            version version
            name "RunDeck Bundled Plugins"
            url 'http://rundeck.org'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            modules{
                module 'stub-plugin'
                module 'script-plugin'
                module 'localexec-plugin'
                module 'copyfile-plugin'
            }
            dependencies{
                dependency{
                    artifactId 'rundeck-core'
                    groupId project.group
                    version version
                }
            }
            build{
                plugins{
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-jar-plugin'
                        version '2.3.2'
                        configuration{
                            archive{
                              manifestEntries{
                                'Rundeck-Plugin-Version'(rundeckPluginVersion)
                                'Rundeck-Plugin-Archive'('true')
                                'Rundeck-Plugin-File-Version'(version)
                              }
                            }
                        }
                    }

                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-compiler-plugin'
                        version '2.3.2'
                        configuration{
                            'source'('1.7')
                            'target'('1.7')
                        }
                    }
                }
            }
        }
    }.writeTo("pom.xml")
}

