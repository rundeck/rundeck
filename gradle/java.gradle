import org.gradle.plugins.signing.Sign
/**
 * Common build settings for java projects
 */
apply plugin: 'java';
apply plugin: 'maven';
apply plugin: 'eclipse';
apply plugin: 'idea';

sourceCompatibility = 1.8
targetCompatibility = 1.8

// add static analysis support when running on build servers (-Penvironment=build)
if (project.hasProperty('environment') && environment == 'build') {
    if(project in rootProject.exportedProjects.collect{project(it)}){
        apply from: rootDir.path + '/gradle/cobertura.gradle'
        apply plugin: 'findbugs'

        findbugs {
            toolVersion = '3.0.1'
            ignoreFailures = true
        }
        if (project.hasProperty('findbugsHtml')) {
            tasks.withType(org.gradle.api.plugins.quality.FindBugs) {
                reports {
                    xml.enabled false
                    html.enabled true
                }
            }
        }
    }
}

repositories {
    mavenLocal()
    maven {
        url mavenCentralUrl
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            credentials {
                username mavenUser
                password mavenPassword
            }
        }
    }
}


// set the eclipse project naming convention to rundeck:<path>:<projectName>
// so it matches the logical hierarchy more closely
eclipse.project.name = "${project.getParent().eclipse.project.name}:${name}"

// produce a jar file for our test classes
task testJar(type: Jar) {
    classifier = 'tests'
    from sourceSets.test.java
}

// produce a jar file for our source files
task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java
}

// produce a jar file for our javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

// add all of the artifacts above to our archives list
artifacts {
    archives testJar
    archives sourceJar
    archives javadocJar
}
