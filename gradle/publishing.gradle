apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        "${project.name}"(MavenPublication) { publication ->
            if (project.ext.hasProperty('publishShadow') && project.ext.publishShadow) {
                project.shadow.component(publication)
            } else {
                from components.java
            }

            pom {
                name = publishName
                description = project.ext.hasProperty('publishDescription') ? project.ext.publishDescription :
                              project.description ?: publishName
                url = "https://github.com/${githubSlug}"
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    url = "https://github.com/${githubSlug}"
                    connection = "scm:git:git@github.com/${githubSlug}.git"
                    developerConnection = "scm:git:git@github.com:${githubSlug}.git"
                }
                if (project.ext.developers) {
                    developers {
                        project.ext.developers.each { dev ->
                            developer {
                                id = dev.id
                                name = dev.name
                                email = dev.email
                            }
                        }
                    }
                }
            }

        }
    }
}
def base64Decode = { String prop ->
    project.findProperty(prop) ?
    new String(Base64.getDecoder().decode(project.findProperty(prop).toString())).trim() :
    null
}

if (project.hasProperty('signingKey') && project.hasProperty('signingPassword')) {
    signing {
        useInMemoryPgpKeys(base64Decode("signingKey"), project.signingPassword)
        sign(publishing.publications)
    }
}