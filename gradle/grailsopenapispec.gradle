/**
 * Sets up artifacts and configurations for the openapi spec generation for grails plugins
 */
configurations{
    apiSpecFiles{
        canBeConsumed = true
        canBeResolved = false
    }
}

tasks.withType(GroovyCompile).configureEach {
    def opts=['-Xmx1024m']
    if(isRunning){
        //disable openapi spec generation during bootRun
        opts.add('-Dmicronaut.openapi.enabled=false')
    }else{
        outputs.file(file("${buildDir}/classes/groovy/main/META-INF/swagger/api-${project.name}.yml"))
        opts.add "-Dmicronaut.openapi.target.file=${buildDir}/classes/groovy/main/META-INF/swagger/api-${project.name}.yml".toString()
    }
    configure(groovyOptions) {
        forkOptions.jvmArgs = opts
    }
}
artifacts{
    //note: both builtBy tasks seem to declare outputs which include the openapi spec file, so we include both otherwise
    //gradle warns about implicit dependencies
    apiSpecFiles file: file("${buildDir}/classes/groovy/main/META-INF/swagger/api-${project.name}.yml"), name: "${project.name}", type: 'file', builtBy: compileGroovy
    apiSpecFiles file: file("${buildDir}/classes/groovy/main/META-INF/swagger/api-${project.name}.yml"), name: "${project.name}", type: 'file', builtBy: copyAstClasses
}

dependencies{
    compileOnly "io.swagger.core.v3:swagger-annotations:${swaggerVersion}"
    compileOnly("io.micronaut.openapi:micronaut-openapi:${micronautOpenapiVersion}") {
        exclude group: 'org.slf4j', module: 'slf4j-nop'
    }
    compileOnly "io.micronaut:micronaut-http-server:${micronautVersion}"
    compileOnly "io.micronaut:micronaut-inject:${micronautVersion}"
    compileOnly "io.micronaut:micronaut-inject-java:${micronautVersion}"
    compileOnly "io.micronaut:micronaut-inject-groovy:${micronautVersion}"
    compileOnly "io.micronaut:micronaut-core:${micronautVersion}"
}