/**
 * Sets up artifacts and configurations for the openapi spec generation for grails plugins
 */
configurations{
    apiSpecFiles{
        canBeConsumed = true
        canBeResolved = false
    }
}

tasks.withType(GroovyCompile).configureEach {
    // Skip OpenAPI processing for GSP compilation tasks (incompatible with Groovy 4)
    if (name.contains('GroovyPages') || name == 'compileGroovyPages') {
        // Explicitly disable Micronaut annotation processing for GSP tasks
        configure(groovyOptions) {
            forkOptions.jvmArgs = ['-Xmx1024m', '-Dmicronaut.openapi.enabled=false']
        }
        return
    }
    
    def opts=['-Xmx1024m']
    if(isRunning){
        //disable openapi spec generation during bootRun
        opts.add('-Dmicronaut.openapi.enabled=false')
    }else{
        outputs.file(file("${buildDir}/classes/groovy/main/META-INF/swagger/api-${project.name}.yml"))
        opts.add "-Dmicronaut.openapi.target.file=${buildDir}/classes/groovy/main/META-INF/swagger/api-${project.name}.yml".toString()
    }
    // Allow OpenAPI annotation warnings without failing the build
    options.compilerArgs.add('-Xlint:-processing')
    configure(groovyOptions) {
        forkOptions.jvmArgs = opts
    }
}

// Disable Micronaut AST processing for GSP compilation (JavaExec tasks)
// Prevents "Cannot load from object array because ClassNode.getGenericsTypes() is null" error
// This is a known Groovy 4.0.29 + Micronaut AST transformation compatibility issue
// GSP compilation uses JavaExec, not GroovyCompile, so we need to configure it separately
tasks.withType(JavaExec).configureEach {
    if (name == 'compileGroovyPages') {
        // Disable Micronaut OpenAPI processing to prevent AST transformation conflicts
        // This prevents Micronaut's AST visitor from processing class nodes during GSP compilation
        jvmArgs += ['-Dmicronaut.openapi.enabled=false']
    }
}
artifacts{
    //note: both builtBy tasks seem to declare outputs which include the openapi spec file, so we include both otherwise
    //gradle warns about implicit dependencies
    apiSpecFiles file: file("${buildDir}/classes/groovy/main/META-INF/swagger/api-${project.name}.yml"), name: "${project.name}", type: 'file', builtBy: compileGroovy
    apiSpecFiles file: file("${buildDir}/classes/groovy/main/META-INF/swagger/api-${project.name}.yml"), name: "${project.name}", type: 'file', builtBy: copyAstClasses
}

// Fix Gradle 8 task ordering - test tasks must run after integration test compilation
tasks.named("test").configure {
    mustRunAfter tasks.named("compileIntegrationTestGroovy")
}

dependencies{
    compileOnly "io.swagger.core.v3:swagger-annotations:${swaggerVersion}"
    compileOnly("io.micronaut.openapi:micronaut-openapi:${micronautOpenapiVersion}") {
        exclude group: 'org.slf4j', module: 'slf4j-nop'
    }
    compileOnly "io.micronaut:micronaut-http-server:${micronautVersion}"
    compileOnly "io.micronaut:micronaut-inject"
    compileOnly "io.micronaut:micronaut-inject-java:${micronautVersion}"
    compileOnly "io.micronaut:micronaut-inject-groovy:${micronautVersion}"
    compileOnly "io.micronaut:micronaut-core"
}