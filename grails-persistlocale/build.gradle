buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath platform("org.apache.grails:grails-bom:$grailsVersion")
        classpath "org.apache.grails:grails-gradle-plugins:$grailsVersion"
    }
}

plugins {
    id "org.springframework.boot" version "${springBootVersion}"
}

group "org.rundeck.grails.plugins.persistlocale"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.apache.grails.gradle.grails-plugin"
apply plugin:"org.apache.grails.gradle.grails-gsp"

apply from: "../gradle/java-version.gradle"

repositories {
    mavenLocal()
        mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    implementation ("org.apache.grails:grails-core") {
        exclude(group:"org.yaml",module:"snakeyaml")
    }
    implementation "org.springframework.boot:spring-boot-starter-log4j2"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.apache.grails:grails-web-boot"
    implementation "org.apache.grails:grails-logging"
    implementation "org.apache.grails:grails-rest-transforms"
    implementation "org.apache.grails:grails-databinding"
    implementation "org.apache.grails:grails-core"
    implementation "org.apache.grails:grails-services"
    implementation "org.apache.grails:grails-url-mappings"
    implementation "org.apache.grails:grails-interceptors"
    implementation "org.apache.grails:grails-async"
    implementation "org.apache.grails:grails-scaffolding"
    implementation "org.apache.grails:grails-gsp"
    implementation "org.yaml:snakeyaml:${snakeyamlVersion}"
    implementation "commons-io:commons-io:${commonsIoVersion}"
    implementation "jakarta.annotation:jakarta.annotation-api:${jakartaAnnotationVersion}"
    compileOnly "io.micronaut:micronaut-inject-groovy:${micronautVersion}"
    console "org.apache.grails:grails-console"
    profile ("org.grails.profiles:web-plugin"){
        exclude(group:"org.grails.profiles",module:"web")
    }
    runtimeOnly "cloud.wondrify:asset-pipeline-grails:$assetPluginVersion"
    testImplementation "org.apache.grails:grails-testing-support-datamapping"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.apache.grails:grails-testing-support-web"
    
    constraints {
        implementation("commons-io:commons-io:${commonsIoVersion}") {
            because "Fix CVE-2024-47554 - force newer version over transitive dependencies"
        }
        implementation("commons-fileupload:commons-fileupload:${commonsFileuploadVersion}") {
            because "Fix CVE-2025-48976 - force newer version over transitive dependencies from grails-web-fileupload"
        }
    }
}

bootRun {
    ignoreExitValue true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always',
        '-noverify',
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

// enable if you wish to package this plugin as a standalone application
bootJar.enabled = false
