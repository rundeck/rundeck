buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.apache.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
    }
}

plugins {
    id "org.springframework.boot" version "${springBootVersion}"
}

group "org.rundeck.grails.plugins.persistlocale"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-gsp"

apply from: "../gradle/java-version.gradle"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    implementation ("org.apache.grails:grails-core") {
        exclude(group:"org.yaml",module:"snakeyaml")
    }
    implementation "org.springframework.boot:spring-boot-starter-log4j2"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.apache.grails:grails-web-boot"
    implementation "org.apache.grails:grails-logging"
    implementation "org.apache.grails:grails-plugin-rest"
    implementation "org.apache.grails:grails-plugin-databinding"
    implementation "org.apache.grails:grails-plugin-i18n"
    implementation "org.apache.grails:grails-plugin-services"
    implementation "org.apache.grails:grails-plugin-url-mappings"
    implementation "org.apache.grails:grails-plugin-interceptors"
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:scaffolding"
    implementation "org.grails.plugins:gsp"
    implementation "org.yaml:snakeyaml:${snakeyamlVersion}"
    implementation "commons-io:commons-io:${commonsIoVersion}"
    implementation "io.micronaut:micronaut-inject-groovy"
    console "org.apache.grails:grails-console"
    profile ("org.grails.profiles:web-plugin"){
        exclude(group:"org.grails.profiles",module:"web")
    }
    runtimeOnly "com.bertramlabs.plugins:asset-pipeline-grails:$assetPluginVersion"
    testImplementation "org.apache.grails:grails-gorm-testing-support"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.apache.grails:grails-web-testing-support"
    
    constraints {
        implementation("commons-io:commons-io:${commonsIoVersion}") {
            because "Fix CVE-2024-47554 - force newer version over transitive dependencies"
        }
        implementation("commons-fileupload:commons-fileupload:${commonsFileuploadVersion}") {
            because "Fix CVE-2025-48976 - force newer version over transitive dependencies from grails-web-fileupload"
        }
    }
}

bootRun {
    ignoreExitValue true
    jvmArgs(
        '-Dspring.output.ansi.enabled=always',
        '-noverify',
        '-XX:TieredStopAtLevel=1',
        '-Xmx1024m')
    sourceResources sourceSets.main
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Xmx1024m']
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

// enable if you wish to package this plugin as a standalone application
bootJar.enabled = false
