/**
 * The Rundeck launcher app build file
 */

description = 'This is the launcher application which wraps the Jetty server'

apply plugin:'application'

mainClassName = 'com.dtolabs.rundeck.ExpandRunServer'

archivesBaseName = 'rundeck-launcher'
defaultTasks 'clean','launcherJar'
ext.launcherMainClass = mainClassName
ext.launcherStartClass = "com.dtolabs.rundeck.RunServer"
ext.launcherJettyLibs = "servlet-api-2.5-20081211.jar jetty-${jettyVersion}.jar jetty-util-${jettyVersion}.jar jetty-naming-${jettyVersion}.jar jetty-plus-${jettyVersion}.jar jna-3.2.2.jar libpam4j-1.5.jar not-yet-commons-ssl-0.3.11.jar"
ext.launcherJettyLibsUnexpanded = 'servlet-api-2.5-20081211.jar jetty-${jettyVersion}.jar jetty-util-${jettyVersion}.jar jetty-naming-${jettyVersion}.jar jetty-plus-${jettyVersion}.jar jna-3.2.2.jar libpam4j-1.5.jar not-yet-commons-ssl-0.3.11.jar'
ext.launcherJettyLibPath = "pkgs/webapp/WEB-INF/lib"

ext.launcherContentsDir = new File("$projectDir/build/launcher-contents")

eclipse.project.name = "${rootProject.eclipse.project.name}:launcher:app";

configurations{
    warBundle
    jettyServerLib
    pluginFiles{
        transitive = false
    }
}

dependencies {
    compile project(":rundeck-launcher:rundeck-jetty-server"),
		'commons-cli:commons-cli:1.0'
	
    warBundle project(':rundeckapp')
	
    pluginFiles project(':plugins:script-plugin'),
        project(':plugins:stub-plugin'),
        project(':plugins:localexec-plugin')

    jettyServerLib(project(path:":rundeck-launcher:rundeck-jetty-server", configuration:'runtime')) {
        exclude module:'commons-cli'
        transitive = true
    }
    jettyServerLib("org.kohsuke:libpam4j:1.5"){
        transitive = true
    }
}

task setupLauncherContent << {
    launcherContentsDir.mkdirs()
}

task expandCli(dependsOn: setupLauncherContent) << {
    //copy libs to a tools lib dir
    def dep = configurations.compile.allDependencies.find { dep -> dep.name == 'commons-cli' }
    FileTree cliJar = zipTree(configurations.compile.files(dep).find{it})
    copy{
        from cliJar
        exclude 'META-INF/**'
        into launcherContentsDir
    }
}

task expandWar(dependsOn: setupLauncherContent) << {
    //copy libs to a tools lib dir
    File webappDir=new File(launcherContentsDir,"pkgs/webapp")
    webappDir.mkdirs()
    FileTree rundeckWar = zipTree(configurations.warBundle.files.find{ file -> file})
    copy{
        from rundeckWar
        into webappDir
    }
}

task copyJettyServerLib(dependsOn: setupLauncherContent) << {
    File libDir=new File(launcherContentsDir,"lib")
    libDir.mkdirs()
    def serverlibjar = configurations.jettyServerLib.files
    copy{
        from serverlibjar
        into libDir
    }
}

task copyPluginLibs(dependsOn: setupLauncherContent) << {
    File libextDir=new File(launcherContentsDir,"libext")
    libextDir.mkdirs()
    configurations.pluginFiles.files.each{ pluginFile->
        copy{
            from pluginFile
            into libextDir
        }
    }
}

jar {
    from launcherContentsDir
    from sourceSets.main.output
    exclude 'com/dtolabs/rundeck/RunServer.class'
    manifest {
        attributes 'Rundeck-Version': version, 'Main-Class':"${launcherMainClass}", "Rundeck-Start-Class":"${launcherStartClass}", "Rundeck-Jetty-Libs":"${launcherJettyLibs}", "Rundeck-Jetty-Lib-Path":"${launcherJettyLibPath}"
    }
}
jar.dependsOn(expandCli, expandWar, copyJettyServerLib, copyPluginLibs)

apply plugin: 'maven'
task createPom << {
    pom {
        project {
            artifactId archivesBaseName
            groupId project.group
            inceptionYear '2011'
            packaging 'jar'
            version version
            name "Rundeck Launcher"
            url 'http://rundeck.org'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            properties{
                'jettyVersion'(jettyVersion)
                'launcherStartClass'(launcherStartClass)
                'launcherMainClass'(launcherMainClass)
                'launcherJettyLibPath'(launcherJettyLibPath)
            }
            build {
                plugins{
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-compiler-plugin'
                        version '2.3.2'
                        configuration{
                            'source'('1.5')
                            'target'('1.5')
                        }
                    }
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-assembly-plugin'
                        version '2.2.2'
                        configuration{
                            appendAssemblyId('false')
                            descriptors{
                                descriptor('src/main/assembly/src.xml')
                            }
                            archive{
                              manifestEntries{
                                'Rundeck-Version'('${project.version}')  
                                'Rundeck-Start-Class'('${launcherStartClass}')
                                'Main-Class'('${launcherMainClass}')
                                'Rundeck-Jetty-Libs'(launcherJettyLibsUnexpanded)
                                'Rundeck-Jetty-Lib-Path'('${launcherJettyLibPath}')
                              }
                            }
                        }
                        executions{
                            execution{
                                id 'make-assembly'
                                phase 'package'
                                goals{
                                    goal 'single'
                                }
                            }
                        }
                    }
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-jar-plugin'
                        version '2.3.2'
                        configuration{
                            classifier 'code'
                        }
                    }
                }
            }
            dependencies{
                (configurations.warBundle.dependencies).each{ dep ->
                    dependency{
                        groupId dep.group
                        artifactId 'rundeck' //override gradle project name
                        version dep.version
                        type 'war'
                    }
                }
                (configurations.pluginFiles.dependencies).each{ dep ->
                    dependency{
                        groupId dep.group
                        artifactId "rundeck-${dep.name}" //override gradle project name
                        version dep.version
                        type 'jar'
                    }
                }
//                (configurations.jettyServerLib.dependencies).each{ dep ->
//                    dependency{
//                        groupId dep.group
//                        artifactId dep.name
//                        version dep.version
//                        type 'jar'
//                    }
//                }
            }
        }
    }.writeTo("pom.xml")
}

