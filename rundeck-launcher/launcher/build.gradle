/*
 * Copyright 2016 SimplifyOps, Inc. (http://simplifyops.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * The Rundeck launcher app build file
 */

description = 'This is the launcher application which wraps the Jetty server'

apply plugin:'application'

mainClassName = 'com.dtolabs.rundeck.ExpandRunServer'

archivesBaseName = 'rundeck-launcher'
defaultTasks 'clean','launcherJar'
ext.launcherMainClass = mainClassName
ext.launcherStartClass = "com.dtolabs.rundeck.RunServer"
ext.launcherJettyLibs = "javax.servlet-3.0.0.v201112011016.jar jetty-${jettyVersion}.jar jetty-security-${jettyVersion}.jar jetty-server-${jettyVersion}.jar jetty-util-${jettyVersion}.jar jetty-io-${jettyVersion}.jar jetty-http-${jettyVersion}.jar jetty-jaas-${jettyVersion}.jar jna-platform-${jnaVersion}.jar jna-${jnaVersion}.jar libpam4j-1.5.jar not-yet-commons-ssl-0.3.17.jar"
ext.launcherJettyLibsUnexpanded = 'javax.servlet-3.0.0.v201112011016.jar jetty-${jettyVersion}.jar jetty-security-${jettyVersion}.jar jetty-server-${jettyVersion}.jar jetty-util-${jettyVersion}.jar jetty-io-${jettyVersion}.jar jetty-http-${jettyVersion}.jar jetty-jaas-${jettyVersion}.jar jna-platform-${jnaVersion}.jar jna-${jnaVersion}.jar libpam4j-1.5.jar not-yet-commons-ssl-0.3.17.jar'
ext.launcherJettyLibPath = "pkgs/webapp/WEB-INF/lib"

ext.launcherContentsDir = new File("$projectDir/build/launcher-contents")

eclipse.project.name = "${rootProject.eclipse.project.name}:launcher:app";

configurations{
    warBundle
    jettyServerLib
}

dependencies {
    compile project(":rundeck-launcher:rundeck-jetty-server"),
		'commons-cli:commons-cli:1.0'
	
    warBundle project(path: ':rundeck', configuration: 'war')
	

    jettyServerLib(project(path:":rundeck-launcher:rundeck-jetty-server", configuration:'runtime')) {
        exclude module:'commons-cli'
        transitive = true
    }
    jettyServerLib("org.kohsuke:libpam4j:1.5"){
        transitive = true
    }
}

task('setupLauncherContent').doLast {
    launcherContentsDir.mkdirs()
}

task('expandCli').dependsOn(setupLauncherContent).doLast {
    //copy libs to a tools lib dir
    def dep = configurations.compile.allDependencies.find { dep -> dep.name == 'commons-cli' }
    FileTree cliJar = zipTree(configurations.compile.files(dep).find{it})
    copy{
        from cliJar
        exclude 'META-INF/**'
        into launcherContentsDir
    }
}

task('expandWar').dependsOn(setupLauncherContent).doLast {
    //copy libs to a tools lib dir
    File webappDir=new File(launcherContentsDir,"pkgs/webapp")
    webappDir.mkdirs()
    FileTree rundeckWar = zipTree(configurations.warBundle.files.find{ file -> file})
    copy{
        from rundeckWar
        into webappDir
    }
}

task('copyJettyServerLib').dependsOn(setupLauncherContent).doLast {
    File libDir=new File(launcherContentsDir,"lib")
    libDir.mkdirs()
    def serverlibjar = configurations.jettyServerLib.files
    copy{
        from serverlibjar
        into libDir
    }
}

jar {
    from launcherContentsDir
    from sourceSets.main.output
    exclude 'com/dtolabs/rundeck/RunServer.class'
    manifest {
        attributes 'Rundeck-Version': version, 'Main-Class':"${launcherMainClass}", "Rundeck-Start-Class":"${launcherStartClass}", "Rundeck-Jetty-Libs":"${launcherJettyLibs}", "Rundeck-Jetty-Lib-Path":"${launcherJettyLibPath}"
    }
}
jar.dependsOn(expandCli, expandWar, copyJettyServerLib)

apply plugin: 'maven'
task('createPom').doLast {
    pom {
        project {
            artifactId archivesBaseName
            groupId project.group
            inceptionYear '2011'
            packaging 'jar'
            version version
            name "Rundeck Launcher"
            url 'http://rundeck.org'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            properties{
                'jettyVersion'(jettyVersion)
                'launcherStartClass'(launcherStartClass)
                'launcherMainClass'(launcherMainClass)
                'launcherJettyLibPath'(launcherJettyLibPath)
            }
            build {
                plugins{
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-compiler-plugin'
                        version '2.3.2'
                        configuration{
                            'source'('1.7')
                            'target'('1.7')
                        }
                    }
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-assembly-plugin'
                        version '2.2.2'
                        configuration{
                            appendAssemblyId('false')
                            descriptors{
                                descriptor('src/main/assembly/src.xml')
                            }
                            archive{
                              manifestEntries{
                                'Rundeck-Version'('${project.version}')  
                                'Rundeck-Start-Class'('${launcherStartClass}')
                                'Main-Class'('${launcherMainClass}')
                                'Rundeck-Jetty-Libs'(launcherJettyLibsUnexpanded)
                                'Rundeck-Jetty-Lib-Path'('${launcherJettyLibPath}')
                              }
                            }
                        }
                        executions{
                            execution{
                                id 'make-assembly'
                                phase 'package'
                                goals{
                                    goal 'single'
                                }
                            }
                        }
                    }
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-jar-plugin'
                        version '2.3.2'
                        configuration{
                            classifier 'code'
                        }
                    }
                }
            }
            dependencies{
                (configurations.warBundle.dependencies).each{ dep ->
                    dependency{
                        groupId dep.group
                        artifactId 'rundeck' //override gradle project name
                        version dep.version
                        type 'war'
                    }
                }
//                (configurations.jettyServerLib.dependencies).each{ dep ->
//                    dependency{
//                        groupId dep.group
//                        artifactId dep.name
//                        version dep.version
//                        type 'jar'
//                    }
//                }
            }
        }
    }.writeTo("pom.xml")
}

