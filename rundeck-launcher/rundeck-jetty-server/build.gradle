/**
 * The Rundeck Jetty server build file
 */

description = 'This is the embedded Jetty server project'

archivesBaseName = 'rundeck-jetty-server'

// set the convention to rundeck:<branch>:<path>:<projectName>
eclipse.project.name =  "${rootProject.eclipse.project.name}:launcher:server";

dependencies {
    compile( 
        [group: 'log4j', name: 'log4j', version: '1.2.16',ext:'jar'],
        [group: 'commons-cli', name: 'commons-cli', version: '1.0',ext:'jar'],
        [group: 'org.eclipse.jetty.aggregate', name: 'jetty-all', version: jettyVersion,ext:'jar'],
        [group: 'org.kohsuke', name:'libpam4j', version:'1.5', ext:'jar'],
        // This is in httpcomponents 4+ but stuck on 3.x for now.
        [group: 'ca.juliusdavies', name: 'not-yet-commons-ssl', version: '0.3.11', ext: 'jar'],
    )
    runtime(
        [group: 'org.eclipse.jetty.aggregate', name: 'jetty-all', version: jettyVersion, ext: 'jar'],
        [group: 'javax.servlet', name: 'servlet-api', version: '2.5', ext: 'jar'],
    )
    testCompile (
        [group: 'junit', name: 'junit', version: '4.8.1',ext:'jar'],
        [group: 'org.mockito', name: 'mockito-all', version: '1.8.5',ext:'jar'],
    )
}

jar {
    manifest {
        attributes 'Rundeck-Version': version, 'Main-Class':"com.dtolabs.rundeck.RunServer"
    }
}

install.dependsOn build 

apply plugin: 'maven'
task createPom << {
    pom {
        project {
            artifactId archivesBaseName
            groupId project.group
            inceptionYear '2011'
            packaging 'jar'
            version version
            name "RunDeck Jetty Server"
            url 'http://rundeck.org'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            build {
                plugins{
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-compiler-plugin'
                        version '2.3.2'
                        configuration{
                            'source'('1.7')
                            'target'('1.7')
                        }
                    }
                    plugin{
                        groupId 'org.apache.maven.plugins'
                        artifactId 'maven-jar-plugin'
                        version '2.3.2'
                        configuration{
                            archive{
                              manifestEntries{
                                'Rundeck-Version'(version)
                                'Main-Class'('com.dtolabs.rundeck.RunServer')
                              }
                            }
                        }
                    }
                }
            }
        }
    }.writeTo("pom.xml")
}
