/*
 * Copyright 2016 SimplifyOps, Inc. (http://simplifyops.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "org.dvaske.gradle.git-build-info"
    id 'de.fuerstenau.buildconfig' version '1.1.8'
    id "maven-publish"
}
import java.text.SimpleDateFormat

/**
 * The Rundeck Core API build file
 **/
evaluationDependsOn(':rundeck-storage')
apply plugin: 'idea'
// apply plugin: 'maven'

description = 'The Rundeck Core API project'

archivesBaseName = 'rundeck-core'
defaultTasks 'clean','assemble'
apply plugin: "groovy"
configurations {
    provided
}
sourceSets {
    main { compileClasspath += configurations.provided }
}

tasks.withType(Javadoc) {
    classpath += configurations.provided
}
dependencies {
    compile project(":rundeck-storage:rundeck-storage-api")
    compile project(":rundeck-storage:rundeck-storage-conf")
    compile project(":rundeck-authz:rundeck-authz-api")
    compile project(":rundeck-authz:rundeck-authz-core")
    compile project(":rundeck-authz:rundeck-authz-yaml")
    compile ('com.google.guava:guava:26.0-jre') {
        exclude group:'org.codehaus.mojo', module: 'animal-sniffer-annotations'
        exclude group:'org.checkerframework', module: 'checker-compat-qual'
        exclude group:'com.google.errorprone', module: 'error_prone_annotations'
        exclude group:'com.google.j2objc', module: 'j2objc-annotations'
        exclude group:'com.google.code.findbugs', module: 'jsr305'
    }

    implementation 'org.apache.ant:ant:1.9.15',
            'org.slf4j:slf4j-api:1.7.30',
        'commons-codec:commons-codec:1.11',
        'commons-beanutils:commons-beanutils:1.9.4',
        'commons-collections:commons-collections:3.2.2',
        'commons-lang:commons-lang:2.6',
        'org.dom4j:dom4j:2.1.3',
        'jaxen:jaxen:1.1.6',
        'commons-cli:commons-cli:1.0',
        'org.apache.ant:ant-jsch:1.9.15',
        'com.jcraft:jsch:0.1.55',
        'com.jcraft:jsch.agentproxy.jsch:0.0.9',
        'com.jcraft:jsch.agentproxy.sshagent:0.0.9',
        'com.jcraft:jsch.agentproxy.usocket-jna:0.0.9',
        'com.jcraft:jsch.agentproxy.usocket-nc:0.0.9',
        'com.jcraft:jsch.agentproxy.connector-factory:0.0.9',
        'com.jcraft:jsch.agentproxy.core:0.0.9',
        "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}",
        "org.yaml:snakeyaml:${snakeyamlVersion}",
        'com.squareup.retrofit2:retrofit:2.7.2',
        'com.squareup.retrofit2:converter-jackson:2.7.2',
        'javax.servlet:javax.servlet-api:4.0.1'


    implementation ("org.apache.httpcomponents:httpclient:${httpClientVersion}")
    implementationOnly "org.projectlombok:lombok:1.18.10"
    annotationProcessor "org.projectlombok:lombok:1.18.10"
    testImplementation 'junit:junit:4.8.1',
        'org.mockito:mockito-all:1.10.19'

    testImplementation "org.codehaus.groovy:groovy-all:${groovyVersion}"
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
    testImplementation "com.squareup.retrofit2:retrofit-mock:2.7.2"
    testImplementation "com.squareup.okhttp3:mockwebserver:3.14.0"
    testImplementation "cglib:cglib-nodep:2.2.2"

}
buildConfig {
    clsName = 'BuildConfig'
    packageName = project.group + '.meta'
    charset = 'UTF-8'
    appName = project.name
    version = project.version
    buildConfigField 'String', 'GIT_COMMIT', {
        project.gitCommit
    }
    buildConfigField 'String', 'GIT_BRANCH', {
        project.gitBranch
    }
    buildConfigField 'String', 'GIT_DESCRIPTION', {
        project.gitDescribeInfo
    }
    buildConfigField 'String', 'BUILD_DATE', {
        new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssX").format(new Date())
    }
    buildConfigField 'String', 'BUILD_NUM', buildNum
    buildConfigField 'String', 'BUILD_IDENT', (version + '-' + buildNum)

}

task gitInfo {
    doLast {
        println "HEAD:     $project.gitHead"
        println "Describe: $project.gitDescribeInfo"
        println "Commit:   $project.gitCommit"
        println "Branch:   $project.gitBranch"
        println "Remote:   $project.gitRemote"
    }
}

jar.doFirst {
    manifest {
        attributes 'Rundeck-Version': version
    }
}

task expandTemplate {
    inputs.file "$projectDir/src/main/meta/com/dtolabs/rundeck/core/application.properties"
    outputs.file "$projectDir/src/main/resources/META-INF/com/dtolabs/rundeck/core/application.properties"

    doLast {
        ant.delete(file: "$projectDir/src/main/resources/META-INF/com/dtolabs/rundeck/core/application.properties")
        def datestring = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssX").format(new Date())
        println("Building: $version-$buildNum ($datestring)")
        copy {
            expand('version': version, 'version_build': buildNum, 'version_ident': version + '-' + buildNum, date: datestring)
            from "$projectDir/src/main/meta/com/dtolabs/rundeck/core/application.properties"
            into "$projectDir/src/main/resources/META-INF/com/dtolabs/rundeck/core/"
        }
    }
}
processResources.dependsOn tasks.expandTemplate

test{
    systemProperties 'rdeck.base': "$projectDir/build/rdeck_base"
}

assemble {
    doLast{
        //copy libs to a tools lib dir
        ant.mkdir(dir:"$projectDir/build/tools/lib")
        configurations.runtime.files.each { file ->
            copy{
                from file
                into "$projectDir/build/tools/lib"
            }
        }
    }
}

// make sure that we've rebuilt and assembled before install
// install.dependsOn(build)

//build a pom we reuse for both maven builds and release to sonatype
def buildpom={
    pom {
    project {
        artifactId archivesBaseName
        groupId project.group
        inceptionYear '2011'
        packaging 'jar'
        version version
        name "Rundeck Core"
        description "Core library for the Rundeck web console for command dispatching and job scheduling"
        url 'http://rundeck.org'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        properties{
            'version'(version)
            'version_build'(buildNum)
            'version_ident'(version+'-'+buildNum)
        }
        scm {
            url 'https://github.com/dtolabs/rundeck'
            connection 'scm:git:git@github.com/dtolabs/rundeck.git'
            developerConnection 'scm:git:git@github.com:dtolabs/rundeck.git'
        }
        developers {
          developer {
            id('gschueler')
            name('Greg Schueler')
            email('greg@dtosolutions.com')
          }
        }
        parent {
            groupId('org.sonatype.oss')
            artifactId('oss-parent')
            version('7')
        }
        build {
            resources{
                resource{
                    directory 'src/main/meta'
                    targetPath 'META-INF'
                    filtering=true
                }
                resource{
                    directory 'src/main/resources'
                }
            }
            plugins{
                plugin() {
                  groupId('org.apache.maven.plugins')
                  artifactId('maven-source-plugin')
                  executions() {
                    execution() {
                      id('attach-sources')
                      goals() {
                        goal('jar')
                      }
                    }
                  }
                }
                plugin() {
                  groupId('org.apache.maven.plugins')
                  artifactId('maven-javadoc-plugin')
                  version('2.8')
                  configuration() {
                    docletArtifact() {
                      groupId('com.google.doclava')
                      artifactId('doclava')
                      version('1.0.3')
                    }
                    doclet('com.google.doclava.Doclava')
                    /* bootclasspath required by Sun's JVM */
                    bootclasspath('${sun.boot.class.path}')
                    additionalparam('''-quiet
                             -federate JDK http://download.oracle.com/javase/6/docs/api/index.html?
                             -federationxml JDK http://doclava.googlecode.com/svn/static/api/openjdk-6.xml
                             -hdf project.name "${project.name}"
                             -d .
                             -hide 101
                             -proofread ./proof-read.txt''')
                    useStandardDocletOptions('false')
                    /* Apple's JVM sometimes requires more memory */
                    additionalJOption('-J-Xmx1024m')
                  }
                  executions() {
                    execution() {
                      id('attach-javadoc')
                      goals() {
                        goal('jar')
                      }
                    }
                  }
                }
                plugin{
                    groupId 'org.apache.maven.plugins'
                    artifactId 'maven-compiler-plugin'
                    version '2.3.2'
                    configuration{
                        'source'('1.7')
                        'target'('1.7')
                    }
                }
                plugin{
                    groupId 'org.apache.maven.plugins'
                    artifactId 'maven-surefire-plugin'
                    version '2.10'
                    configuration{
                        systemPropertyVariables{
                            'rdeck.base'('${project.build.directory}/rdeck_base')
                        }
                        redirectTestOutputToFile 'true'
                    }
                }
                plugin{
                    groupId 'org.apache.maven.plugins'
                    artifactId 'maven-jar-plugin'
                    version '2.3.2'
                    configuration{
                        archive{
                          manifestEntries{
                            'Rundeck-Version'(version)
                          }
                        }
                    }
                }
            }
        }
    }
}
}

task('createPom').doLast {
    buildpom().writeTo("pom.xml")
}
