import java.util.jar.JarInputStream
import java.util.regex.Matcher

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://repo.grails.org/grails/restricted" }
    }
    dependencies {
        classpath platform("org.apache.grails:grails-bom:$grailsVersion")
        classpath "org.apache.grails:grails-gradle-plugins"
        classpath "org.apache.grails:grails-data-hibernate5"
        classpath "org.apache.grails:grails-data-hibernate5-dbmigration"
        classpath "cloud.wondrify:asset-pipeline-gradle"
        classpath "gradle.plugin.com.energizedwork:idea-gradle-plugins:1.4"
    }
}
plugins {
    id "org.springframework.boot" version "${springBootVersion}"
    id "org.dvaske.gradle.git-build-info"
    id "com.github.node-gradle.node" version "7.0.1"
    id "java-library"
    id "war"
}

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.apache.grails.gradle.grails-web"
apply plugin: "com.energizedwork.idea-base"
apply plugin:"cloud.wondrify.asset-pipeline"
apply plugin:"org.apache.grails.gradle.grails-gsp"
apply plugin: "maven-publish"
apply plugin: "signing"

allprojects {
    ext["liquibase.version"] = liquibaseVersion
}

// Exclude micronaut-bom - it's not included by default in Grails 7 per docs
// Also prevent old Groovy BOM conflicts
configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            // Block old groovy-bom from Micronaut 3.x
            if (details.requested.group == 'org.codehaus.groovy' && details.requested.name == 'groovy-bom') {
                details.useTarget group: 'org.apache.groovy', name: 'groovy-bom', version: getProperty('groovy.version')
                details.because 'Grails 7 uses Apache Groovy 4.x'
            }
            // Force Spring Integration 6.x for Spring Boot 3.5.8
            if (details.requested.group == 'org.springframework.integration') {
                details.useVersion springIntegrationVersion
                details.because 'Spring Boot 3.5.8 requires Spring Integration 6.x'
            }
            // Force BouncyCastle 1.79 (Spring Boot 3.5+ BOM manages BC and uses 1.80 which breaks Jasypt)
            if (details.requested.group == 'org.bouncycastle') {
                details.useVersion bouncyCastleVersion
                details.because 'BC 1.80 breaks Jasypt encryption - staying on 1.79 until migration'
            }
        }
    }
    exclude group: 'io.micronaut', module: 'micronaut-bom'
}

//list of grails plugins to depend on in various ways
ext.coreGrailsPlugins = [
    ':grails-metricsweb',
    ':grails-persistlocale',
    ':grails-securityheaders',
    ':grails-repository',
    ':grails-webhooks',
    ':grails-execution-mode-timer',
    ':grails-rundeck-data-shared',
    ':grails-job-kill-handler'
]

if(!project.findProperty('isRunning')) {
    //for openapi spec generation, make gradle
    //evaluate each grails plugin to define the
    //apiSpecFiles configurations, before we depend on them.
    //however, it seems to interfere with proper
    //behavior when running "bootRun", so skip it in that case

    //force gradle configuration of core grails plugins
    coreGrailsPlugins.each{
        project.evaluationDependsOn(it)
    }
    //force gradle configuration of extra grails plugins
    project.findProperty('extraGrailsPlugins')?.each{
        project.evaluationDependsOn(it)
    }
}

configurations {
    agent
    war{}
    bootWar{}
    pluginFiles {
        transitive = false
    }
    apiSpecFiles
    spa //dependency on grails-spa
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.junit.jupiter'
        exclude group: 'org.junit.platform'
        exclude group: 'org.apache.groovy', module: 'groovy-test-junit5'
        exclude group: 'org.apache.ant', module: 'ant-junit'
        exclude group: 'org.codehaus.groovy', module: 'groovy-ant'
        exclude group: 'org.grails', module: 'grails-shell'
    }

    implementation.exclude module: "spring-boot-starter-tomcat"
    implementation.exclude module: "spring-boot-cli"

    //avoid error request.XML
    implementation.exclude group: 'pull-parser'

}

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    
    // PackageCloud com.rundeck.plugins repositories
    maven {
        url "https://packagecloud.io/pagerduty/rundeckpro-test/maven2"
    }
    maven {
        url "https://packagecloud.io/pagerduty/rundeckpro/maven2"
    }
}
sourceSets {
    main {
        resources {
            srcDir 'grails-app/migrations'
        }
    }
}
dependencies {
    //Rundeck plugin dependencies
    pluginFiles project.findProperty('bundledPlugins')?:[]

    // Force Jakarta Annotation API 3.0.0 for annotation processing
    annotationProcessor "jakarta.annotation:jakarta.annotation-api:${jakartaAnnotationVersion}"

    // Jakarta Validation implementation for tests
    testImplementation "org.hibernate.validator:hibernate-validator:8.0.1.Final"
    testImplementation "org.glassfish.expressly:expressly:5.0.0" // EL implementation required by Hibernate Validator

    testImplementation group: 'com.squareup.retrofit2', name: 'retrofit-mock', version: '3.0.0'

    spa project(path: ':rundeckapp:grails-spa', configuration: 'spa')

    // Rundeck project dependencies.
    implementation project(':core')
    implementation project(':rundeck-storage:rundeck-storage-filesys')
    implementation project(':rundeck-app-util')


    /** It's important that the configuration addition follow the form 'compile project("")'
     * so the Gradle magic happens. Other forms(ie 'compile proj' or 'compile project(proj)'
     * can produce broken plugins in development mode. */
    for(proj in project.findProperty('bundledGrailsPlugins')) {
        logger.info("Injecting Grails plugin ${proj}")
        implementation project("$proj")
    }

    //api spec file dependencies from extra grails plugins
    for(proj in project.findProperty('extraGrailsPlugins')) {
        if(project("$proj").configurations.findByName('apiSpecFiles')) {
            apiSpecFiles project(path: proj, configuration: 'apiSpecFiles')
        }
    }
    //api spec file dependencies from core grails plugins
    for(proj in project.findProperty('coreGrailsPlugins')) {
        if(project("$proj").configurations.findByName('apiSpecFiles')) {
            apiSpecFiles project(path: proj, configuration: 'apiSpecFiles')
        }
    }

    // From BuildConfig.groovy.
    implementation 'org.quartz-scheduler:quartz:2.3.2'
    implementation 'org.apache.grails:grails-quartz:4.0.0'
    implementation 'org.grails.plugins:mail:3.0.0'
    
    // Liquibase for compile-time exception classes (runtime provided by Spring Boot)
    compileOnly "org.liquibase:liquibase-core:${liquibaseVersion}"
    
    // Groovy 4: XML parsing is a separate module
    implementation "org.apache.groovy:groovy-xml:${getProperty('groovy.version')}"
    
    // Micronaut integration DISABLED due to BOM incompatibility in Grails 7.0.4:
    // - grails-micronaut:7.0.4 depends on micronaut-spring-bom:5.11.0 (requires Micronaut 5.x)
    // - But Grails 7.0.4 uses Micronaut Platform 4.10.1 (Micronaut 4.x)
    // - No micronaut-spring version exists that works with Micronaut 4.x
    // Per docs: "The default integration of the Micronaut Framework has been removed in Grails 7"
    // TODO: Re-enable when Grails BOM is fixed or upgrade to newer Grails version
    // implementation 'org.apache.grails:grails-micronaut'
    
    // Layout engine for Grails 7 (Sitemesh 2.6.x continuation)
    implementation 'org.apache.grails:grails-layout'

    implementation "org.springframework.cloud:spring-cloud-context:${springCloudContextVersion}"

    //enable greenmail plugin below for testing email behavior
    // https://github.com/gpc/greenmail
    //compile 'org.grails.plugins:greenmail:2.0.0.RC2'

    implementation 'com.atlassian.commonmark:commonmark:0.11.0'
    implementation 'com.atlassian.commonmark:commonmark-ext-gfm-tables:0.10.0'
    implementation ('com.googlecode.owasp-java-html-sanitizer:owasp-java-html-sanitizer:20211018.2'){
        exclude(group:'com.google.guava',module:'guava')
    }
    implementation 'org.owasp.encoder:encoder:1.2.1'
    implementation 'org.grails.plugins:external-config:2.0.0'
    implementation "commons-fileupload:commons-fileupload:${commonsFileuploadVersion}"
    implementation "commons-io:commons-io:${commonsIoVersion}"
    implementation "commons-beanutils:commons-beanutils:${beanutilsVersion}"
    // BOMs for dependency management
    implementation platform("org.apache.grails:grails-bom:$grailsVersion")
    implementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
    implementation platform("org.apache.groovy:groovy-bom:${getProperty('groovy.version')}")
    
    implementation "commons-codec:commons-codec:${commonsCodecVersion}"

    implementation "org.yaml:snakeyaml:${snakeyamlVersion}"
    implementation "com.zaxxer:HikariCP:${hikariVersion}"

    // Defined by create-app
    implementation "org.apache.grails:grails-data-hibernate5"
    // hibernate-jcache needed for Hibernate 5.6.15 caching (Grails 7.0.4 → GORM 7.x → Hibernate 5.6.15 Jakarta)
    implementation "org.hibernate:hibernate-jcache:5.6.15.Final"
    implementation('org.ehcache:ehcache:3.8.1')

    implementation ('org.hibernate.validator:hibernate-validator:8.0.1.Final')

    implementation "org.springframework.boot:spring-boot-starter-log4j2"
    implementation "org.springframework.boot:spring-boot-autoconfigure"

    implementation "org.apache.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "io.micrometer:micrometer-registry-prometheus"  // Prometheus metrics endpoint
    implementation "org.springframework.boot:spring-boot-starter-mail"  // Jakarta Mail API for Spring Boot 3
    // Temporarily removed explicit micronaut-spring-context to see what Spring Security provides
    // implementation "io.micronaut.spring:micronaut-spring-context:4.6.1"

    implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }

    implementation ("org.springframework.boot:spring-boot-starter-jetty") {
        exclude(group:'org.eclipse.jetty.websocket',module:'websocket-server')
        exclude(group:'org.eclipse.jetty.websocket',module:'javax-websocket-server-impl')
    }

    implementation "org.apache.grails:grails-web-boot"
    implementation "org.apache.grails:grails-logging"
    implementation "org.apache.grails:grails-rest-transforms"
    implementation "org.apache.grails:grails-databinding"
    implementation "org.apache.grails:grails-core"
    implementation "org.apache.grails:grails-services"
    implementation "org.apache.grails:grails-url-mappings"
    implementation "org.apache.grails:grails-interceptors"
    implementation 'org.grails.plugins:grails-executor:0.4'
    implementation "org.apache.grails:grails-async"
    implementation "org.apache.grails:grails-scaffolding"
    implementation "org.apache.grails:grails-events"
    implementation "org.apache.grails:grails-gsp"
    implementation "org.apache.grails:grails-codecs"
    implementation "org.apache.grails:grails-cache"
    compileOnly "io.micronaut:micronaut-inject-groovy:${micronautVersion}"
    implementation "com.google.code.gson:gson:${gsonVersion}"
    // Jackson versions managed by Spring Boot BOM
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation "com.fasterxml.jackson.core:jackson-annotations"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-cbor"
    // Jetty JAAS removed - using org.rundeck.jaas implementation instead
    // implementation "org.eclipse.jetty:jetty-jaas"  // REMOVED - Jetty 12 doesn't have this
    implementation "org.eclipse.jetty:jetty-jmx"
    implementation "org.eclipse.jetty:jetty-util"
    implementation "org.eclipse.jetty:jetty-security"
    implementation "org.apache.mina:mina-core:${minaCoreVersion}"
    implementation ("org.apache.grails:grails-spring-security:${grailsSpringSecurityVersion}"){
        exclude(group:'net.sf.ehcache',module:'ehcache')
        exclude(group:'org.grails',module:'grails-core')
        exclude(group:'org.grails.plugins',module:'async')
        exclude(group:'io.micronaut',module:'micronaut-bom')
        // Allow micronaut-spring - version will be forced to 4.5.1 via resolutionStrategy
    }
    implementation 'org.kohsuke:libpam4j:1.11'
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:${jakartaXmlBindVersion}"
    implementation "jakarta.annotation:jakarta.annotation-api:${jakartaAnnotationVersion}"
    implementation 'org.dom4j:dom4j:2.1.3'
    implementation ("org.bouncycastle:bcprov-jdk18on:${bouncyCastleVersion}")
    implementation ("org.bouncycastle:bcpg-jdk18on:${bouncyCastleVersion}")
    // Grails 7: BC 1.80 requires bcutil for CryptlibObjectIdentifiers
    implementation ("org.bouncycastle:bcutil-jdk18on:${bouncyCastleVersion}")
    implementation ("org.apache.grails:grails-console") {
        exclude(group:"org.yaml",module:"snakeyaml")
    }
    implementation "org.apache.grails:grails-data-hibernate5-dbmigration"
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: "${httpClientVersion}"
    profile ("org.grails.profiles:web"){
        exclude(group:"org.grails.profiles",module:"web")
    }

    implementation ("com.jayway.jsonpath:json-path:2.9.0"){
        exclude group:"org.slf4j",module:"slf4j-api"
        exclude group:'org.ow2.asm', module: 'asm'
    }

    implementation ("net.minidev:json-smart:2.5.2")
    
    // OpenApi Dependencies
    compileOnly "io.swagger.core.v3:swagger-annotations:${swaggerVersion}"
    compileOnly("io.micronaut.openapi:micronaut-openapi:${micronautOpenapiVersion}") {
        exclude group: 'org.slf4j', module: 'slf4j-nop'
    }
    implementation "io.micronaut:micronaut-http-server:${micronautVersion}"
    implementation "io.micronaut:micronaut-inject"
    implementation "io.micronaut:micronaut-inject-java:${micronautVersion}"
    compileOnly "io.micronaut:micronaut-inject-groovy:${micronautVersion}"
    implementation "io.micronaut:micronaut-core"

    runtimeOnly 'org.aspectj:aspectjweaver:1.9.7'

    // Database drivers
    runtimeOnly "com.h2database:h2:2.2.220"
    runtimeOnly "com.microsoft.sqlserver:mssql-jdbc:${mssqlJdbcVersion}"
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client:2.7.0'
    runtimeOnly 'org.postgresql:postgresql:42.7.2'
    runtimeOnly 'org.rundeck.hibernate:rundeck-oracle-dialect:1.0.1'

    runtimeOnly "org.apache.tomcat:tomcat-jdbc:9.0.89"
    runtimeOnly "org.springframework:spring-jcl"  // Version managed by Spring Boot BOM
    runtimeOnly "org.apache.groovy:groovy-dateutil"  // Version managed by Grails BOM
    api ("cloud.wondrify:asset-pipeline-grails:$assetPluginVersion")
    runtimeOnly "cloud.wondrify:asset-pipeline-grails:$assetPluginVersion"

    implementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    testImplementation "org.apache.grails:grails-testing-support-datamapping"
    testImplementation "org.mockito:mockito-core"
    testImplementation "org.apache.grails:grails-testing-support-web"
    testImplementation "org.apache.groovy:groovy-xml:${getProperty('groovy.version')}"
    testImplementation "org.apache.groovy:groovy-test:${getProperty('groovy.version')}"
    testImplementation "org.liquibase:liquibase-core:${liquibaseVersion}"
    testImplementation "org.eclipse.jetty.ee10:jetty-ee10-webapp"
    testImplementation "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
    testImplementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    // testImplementation( "org.hibernate:hibernate-jcache:${hibernateVersion}") // Managed by Grails BOM

    testRuntimeOnly "org.seleniumhq.selenium:selenium-chrome-driver:${seleniumJavaVersion}"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-firefox-driver:${seleniumJavaVersion}"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-remote-driver:${seleniumJavaVersion}"
    testRuntimeOnly "org.seleniumhq.selenium:selenium-api:${seleniumJavaVersion}"
    testRuntimeOnly 'org.jetbrains.kotlin:kotlin-stdlib:1.3.70'
    testRuntimeOnly 'org.aspectj:aspectjweaver:1.9.7'
    testRuntimeOnly 'org.junit.vintage:junit-vintage-engine'
    // transitive dependencies constraints.
    constraints {
        // CVE constraints temporarily disabled - will re-enable after Grails 7 baseline is stable
        /*
        implementation("com.mchange:c3p0:${c3p0Version}") {
            because "version brought by quartz affected by CVE-2019-5427"
        }
        implementation ("org.bouncycastle:bcprov-jdk18on:${bouncyCastleVersion}") {
            because "earlier versions affected by exploitable bugs"
        }
        implementation ("org.bouncycastle:bcpg-jdk18on:${bouncyCastleVersion}") {
            because "earlier versions affected by exploitable bugs"
        }
        */
        
        // Keep guava constraint - this is for build compatibility, not CVE
        implementation('com.google.guava:guava') {
            version {
                strictly '32.0.1-jre'
            }
            because "guava 33.4.8 from micronaut-guice-bom causes variant resolution conflicts with exportedLibs"
        }
        implementation('com.google.errorprone:failureaccess') {
            version {
                strictly '1.0.1'
            }
            because "matches guava 32.0.1-jre transitive dependency"
        }
        
        // Temporarily disabled - will re-enable after baseline
        /*
        implementation ("org.yaml:snakeyaml:${snakeyamlVersion}") {
            because "required by rundeck:repository"
        }
        implementation("commons-fileupload:commons-fileupload:${commonsFileuploadVersion}") {
            because "Fix CVE-2025-48976"
        }
        */
    }



}

// Inject bundled Grails plugins
coreGrailsPlugins.each {
    dependencies.add('implementation', project(it))
}

for(proj in project.findProperty('bundledGrailsPlugins')) {
    logger.info("Injecting Grails plugin ${proj}")
    dependencies.add('implementation', project("$proj"))
}

// grails{
//     pathingJar = true  // Deprecated in Grails 7
// }

task copyRuntimeLibs(type: Copy) {
    from configurations.pluginFiles
    into "${projectDir}/rundeck-runtime/libext"
}

bootRun {
    dependencies{
        coreGrailsPlugins.each{
            implementation project(it)
        }
    }
    dependsOn 'copySpa', copyRuntimeLibs, 'vendorAssetDeps'
    main 'rundeckapp.Application'
    ignoreExitValue true
    jvmArgs(
            '-Dspring.output.ansi.enabled=always',
            '-noverify',
            '-XX:TieredStopAtLevel=1',
            '-Xmx2048m')
    systemProperties(System.properties)
    String springProfilesActive = 'spring.profiles.active'
    systemProperty springProfilesActive, System.getProperty(springProfilesActive)
    systemProperty "build.ident", project.version
    sourceResources sourceSets.main
    //    addResources = true
    System.setProperty("logRoot",System.getProperty("rdeck.base",System.getProperty("user.dir")+"/rundeck-runtime"))
    systemProperty "rundeck.server.logDir", System.getProperty("logRoot")+"/server/logs"
}


tasks.register('copyApiSpecFiles', Copy){
    description 'Copy openapi spec files from each grails plugin dependency into a common dir'
    def additionalSpecDir = file("${buildDir}/openapi")
    doFirst {
        additionalSpecDir.mkdirs()
    }
    outputs.dir additionalSpecDir
    inputs.files configurations.apiSpecFiles
    from configurations.apiSpecFiles
    into additionalSpecDir
    
    // Fix Gradle 8 task ordering - ensure compile tasks run first
    mustRunAfter(':grails-webhooks:compileGroovy')
    mustRunAfter(':grails-webhooks:compileTestGroovy')
    mustRunAfter(':grails-webhooks:compileIntegrationTestGroovy')
    mustRunAfter(':grails-execution-mode-timer:compileGroovy')
    mustRunAfter(':grails-execution-mode-timer:compileTestGroovy')
    mustRunAfter(':grails-execution-mode-timer:compileIntegrationTestGroovy')
    mustRunAfter(':grails-repository:compileGroovy')
    mustRunAfter(':grails-job-kill-handler:compileGroovy')
    mustRunAfter(':grails-metricsweb:compileGroovy')
    
    // Add Pro plugin compile dependencies (only those with integration tests)
    // Only add these dependencies if the plugins exist (Pro repo)
    def proPluginsWithIntTests = [
        'rundeckpro-calendars',
        'rundeckpro-cluster',
        'rundeckpro-config',
        'rundeckpro-genai',
        'rundeckpro-job-tags',
        'rundeckpro-jobdata',
        'rundeckpro-jobqueue',
        'rundeckpro-license',
        'rundeckpro-nodehealthcheck',
        'rundeckpro-paged-job-list',
        'rundeckpro-roimetrics',
        'rundeckpro-runner',
        'rundeckpro-security',
        'rundeckpro-system-report',
        'rundeckpro-tour-manager',
        'rundeckpro-webhooks'
    ]
    
    proPluginsWithIntTests.each { pluginName ->
        def pluginPath = ":plugins:${pluginName}"
        // Only add mustRunAfter if the project exists
        if (project.findProject(pluginPath)) {
            mustRunAfter("${pluginPath}:compileGroovy")
            mustRunAfter("${pluginPath}:compileTestGroovy")
            mustRunAfter("${pluginPath}:compileIntegrationTestGroovy")
        }
    }
}

tasks.register('fixOpenApiSpec') {
    description 'Fix OpenAPI spec to populate version server variable default value'
    def specFile = file("${buildDir}/classes/groovy/main/META-INF/swagger/rundeck-api.yml")
    def apiVersionsFile = file("${projectDir}/src/main/groovy/com/dtolabs/rundeck/app/api/ApiVersions.groovy")
    
    inputs.file(specFile)
    inputs.file(apiVersionsFile)
    outputs.file(specFile)
    
    // Gradle 8: Explicit task ordering - run after main compilation
    mustRunAfter compileGroovy
    mustRunAfter compileJava
    
    doLast {
        if (specFile.exists() && apiVersionsFile.exists()) {
            // Parse the ApiVersions.groovy source file to extract the version
            def apiVersion = null
            apiVersionsFile.eachLine { line ->
                if (line.contains('API_CURRENT_VERSION_STR') && line.contains('=')) {
                    // Extract version from: public final static String API_CURRENT_VERSION_STR = "56"
                    def matcher = line =~ /"(\d+)"/
                    if (matcher) {
                        apiVersion = matcher[0][1]
                    }
                }
            }
            
            if (!apiVersion) {
                throw new GradleException("Could not extract API version from ApiVersions.groovy")
            }
            
            // Simple string replacement approach: replace placeholder with actual version
            // The spec file should contain "$API_VERSION$" as a placeholder (e.g., in server variable default)
            def content = specFile.text
            
            // Replace all occurrences of $API_VERSION$ placeholder with the actual version
            // Note: Wrapped in quotes to ensure YAML treats it as a string (OpenAPI spec requires string type)
            content = content.replaceAll('\\$API_VERSION\\$', "'${apiVersion}'")
            
            specFile.text = content
            logger.info("Successfully updated OpenAPI spec version to ${apiVersion}")
        }
    }
}

tasks.withType(GroovyCompile).configureEach {
    // Disable incremental compilation - incompatible with annotation processors in Gradle 8
    options.incremental = false
    
    def additionalSpecDir = new File(project.buildDir, 'openapi')
    def opts=[
        '-Xmx1024m'
    ]
    if(!project.findProperty('isRunning')) {
        dependsOn copyApiSpecFiles
        finalizedBy fixOpenApiSpec
        opts.addAll([
            '-Dmicronaut.openapi.filename=rundeck-api',
            "-Dmicronaut.openapi.additional.files=${additionalSpecDir.absolutePath}".toString()
        ])
    }else{
        opts.add('-Dmicronaut.openapi.enabled=false')
    }
    configure(groovyOptions) {
        forkOptions.jvmArgs = opts
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    // Increased memory for Groovy 4 + Grails 7 tests
    maxHeapSize = "2048m"
    // Gradle 8: Explicit dependency on fixOpenApiSpec
    dependsOn fixOpenApiSpec
    // Increase Spring Test Context cache size to prevent premature eviction
    // Default is 32, but integration tests need more headroom
    systemProperty 'spring.test.context.cache.maxSize', '256'
    // Also set as JVM arg to ensure it's picked up
    jvmArgs += ['-Dspring.test.context.cache.maxSize=256']
    // Unset GROOVY_HOME to prevent SDKMAN Groovy from interfering with Gradle's test execution
    // Gradle manages its own Groovy dependencies and having GROOVY_HOME set can cause
    // Spring Test Context eviction issues in integration tests
    // Set to empty string to effectively unset it for the test process
    environment['GROOVY_HOME'] = ''
}

// Gradle 8: compileGroovyPages uses fixOpenApiSpec output
tasks.named('compileGroovyPages') {
    dependsOn fixOpenApiSpec
}

// Gradle 8: findMainClass uses fixOpenApiSpec output  
tasks.named('findMainClass') {
    dependsOn fixOpenApiSpec
}

// Gradle 8: javadoc uses fixOpenApiSpec output
tasks.named('javadoc') {
    dependsOn fixOpenApiSpec
}

tasks.withType(Test).configureEach {
    forkEvery = 500
}

// Configure integrationTest task specifically to prevent Spring Test Context eviction
tasks.named('integrationTest').configure {
    // Increase Spring Test Context cache size to prevent premature eviction
    // Default is 32, but integration tests need more headroom
    // Using 512 to handle many test configurations without eviction
    systemProperty 'spring.test.context.cache.maxSize', '512'
    // Also set as JVM arg to ensure it's picked up
    jvmArgs += ['-Dspring.test.context.cache.maxSize=512']
    // Disable parallel execution to prevent context eviction during parallel test runs
    maxParallelForks = 1
    // Unset GROOVY_HOME to prevent SDKMAN Groovy from interfering with Gradle's test execution
    // Gradle manages its own Groovy dependencies and having GROOVY_HOME set can cause
    // Spring Test Context eviction issues in integration tests
    // Set to empty string to effectively unset it for the test process
    environment['GROOVY_HOME'] = ''
}

// Groovy 4: Increase memory for test compilation
tasks.named('compileTestGroovy').configure {
    groovyOptions.forkOptions.with {
        jvmArgs = ['-Xmx3072m', '-XX:MaxMetaspaceSize=768m']
    }
}

// To make Spring boot bootWar task work, we need to declare the duplicateStrategy explicitly
tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

assets {
    minifyJs = false
    minifyCss = false
    excludes = ['gulpfile.js','**/*~','*.json','bootstrap/**/*.less', '_package-manager/**/*']
}

project.gradle.projectsEvaluated {
    buildProperties.doLast {
        File grailsBuildInfoFile = it.outputs.files.files.find { it.name == 'grails.build.info' }
        if (!grailsBuildInfoFile) return
        Properties properties = new Properties()

        grailsBuildInfoFile.withInputStream {
            properties.load(it)
        }

        properties.setProperty("build.ident", properties.getProperty("info.app.version"))
        properties.setProperty("core.version", properties.getProperty("info.app.version"))

        if (project.gitDescribeInfo) {
            properties.setProperty("build.core.git.description", project.gitDescribeInfo)
        }
        if (project.gitCommit) {
            properties.setProperty("build.core.git.commit", project.gitCommit)
        }
        if (project.gitBranch) {
            properties.setProperty("build.core.git.branch", project.gitBranch)
        }

        grailsBuildInfoFile.withOutputStream {
            properties.store(it, null)
        }
    }
}

task('copyPluginLibs').dependsOn(configurations.pluginFiles).doLast {
    println "copying plugin libs"
    File libextDir = new File(buildDir, "WEB-INF/rundeck/plugins")
    File manifestFile = new File(buildDir, "WEB-INF/rundeck/plugins/manifest.properties")

    def filelist=[]
    libextDir.mkdirs()
    configurations.pluginFiles.files.each { pluginFile ->
        project.logger.info("Bundling libExt plugin ${pluginFile.name}")


        copy {
            from pluginFile
            into libextDir
        }
        filelist << pluginFile.name

        /** Mangle JAR plugin properties **/
        if ( ['.jar','.zip'].contains(pluginFile.name[-4..-1]) ) {
            def pluginProps = [:]
            pluginFile.withInputStream {
                def jarmf = new JarInputStream(it).manifest
                jarmf.getMainAttributes().findAll { it.key.toString().startsWith('Rundeck-Plugin') }.each { k, v ->
                    pluginProps[k.toString()] = v
                }
            }
            def f = pluginFile.name
            Properties fileProps = new Properties()
            fileProps['plugin.name'] = pluginProps['Rundeck-Plugin-Name'] ?: f
            fileProps['plugin.description'] = pluginProps['Rundeck-Plugin-Description'] ?: 'Rundeck bundled plugin'
            fileProps['plugin.author'] = pluginProps['Rundeck-Plugin-Author'] ?: 'Rundeck, Inc.'
            fileProps['plugin.version'] = pluginProps['Rundeck-Plugin-File-Version'] ?: version
            fileProps['plugin.url'] = pluginProps['Rundeck-Plugin-URL'] ?: 'http://rundeck.com'
            fileProps['plugin.date'] = pluginProps['Rundeck-Plugin-BuildDate'] ?: (new Date().toString())
            fileProps['plugin.filename'] = f

            //write properties file
            new File(libextDir, pluginFile.name + '.properties').withOutputStream {
                fileProps.store(it, "generated manifest")
            }
        }
    }
    Properties manifestProps = new Properties()
    manifestProps['pluginFileList'] = (filelist.join(','))
    manifestFile.withWriter { w ->
        manifestProps.store(w,"generated manifest")
    }
}

task copySpa(type: Copy) {
    from( configurations.spa){
        include('provided/**')
    }
    into "$projectDir/grails-app/assets"
    outputs.dir "$projectDir/grails-app/assets/provided"
}
assetPluginPackage.dependsOn copySpa
assetCompile.dependsOn copySpa

task vendorAssetDeps(type: NpmTask) { it ->
    dependsOn copySpa
    def baseDir = "${projectDir}/grails-app/assets/javascripts"

    it.inputs.file "${baseDir}/_package-manager/package.json"
    it.inputs.file "${baseDir}/_package-manager/package-lock.json"
    it.inputs.file "${baseDir}/_package-manager/webpack.mix.js"

    it.outputs.dir "${baseDir}/vendor"

    def npmCommand = System.env.CI ?
            'ci:build' :
            'dev:build'

    it.args = ['run', npmCommand]

    execOverrides {
        it.workingDir = "${baseDir}/_package-manager"
    }
}

assetPluginPackage.dependsOn vendorAssetDeps
assetCompile.dependsOn vendorAssetDeps

archivesBaseName = 'rundeck'

tasks.withType(War).configureEach { War war ->
    war.dependsOn compileGroovyPages
}

bootWar {
    enabled=true
    it.dependsOn copyPluginLibs

    from("${buildDir}/WEB-INF/rundeck/plugins") {
        into("WEB-INF/rundeck/plugins")
    }
    from("templates") {
        into("templates")
    }
    manifest {
        attributes(
            ['Add-Opens':'java.management/com.sun.jmx.mbeanserver java.base/java.io java.base/java.lang java.base/java.lang.invoke java.base/java.lang.reflect java.management/java.lang.management java.management/sun.management java.base/java.math java.base/java.net java.base/java.nio.charset java.base/java.nio.file java.base/java.nio.file.attribute java.base/java.security java.base/java.text java.base/java.time java.base/java.time.chrono java.base/java.util java.base/java.util.zip java.base/java.util.concurrent java.base/java.util.concurrent.atomic java.base/java.util.stream java.base/java.util.function java.logging/java.util.logging java.base/java.util.regex java.base/javax.crypto java.base/javax.security.auth java.base/javax.security.auth.login java.base/java.security.cert java.base/sun.nio.cs java.base/sun.nio.fs java.base/sun.security.util java.base/sun.net.www.protocol.jar java.xml/org.xml.sax java.xml/com.sun.org.apache.xerces.internal.impl java.xml/com.sun.org.apache.xerces.internal.dom java.sql/java.sql']
        )
    }
}

artifacts {
    war war
    bootWar bootWar
}

//install.dependsOn assemble
def signArchives = project.tasks.findByName('signArchives')
if (signArchives) {
    bootWar.dependsOn(war)
    signArchives.dependsOn(bootWar)
}

clean {
    delete "$projectDir/grails-app/assets/provided"
    delete "${rootProject.buildDir}/gitbuildinfo/rundeck"
    delete "$projectDir/grails-app/assets/javascripts/vendor"
}

tasks.withType(GenerateMavenPom).all {
    doLast {
        File file
        if(project.name == "rundeckapp"){
            file = new File("$buildDir/publications/rundeck/pom-default.xml")
        }
        else if (project.name == "core") {
            file = new File("$buildDir/publications/rundeck-core/pom-default.xml")
        }
        else{
            file = new File("$buildDir/publications/${project.name}/pom-default.xml")
        }
        def text = file.text
        def pattern = "(?s)(<dependencyManagement>.+?<dependencies>)(.+?)(</dependencies>.+?</dependencyManagement>)"
        Matcher matcher = text =~ pattern
        if (matcher.find()) {
            text = text.replaceFirst(pattern, "")
            def firstDeps = matcher.group(2)
            text = text.replaceFirst(pattern, '$1$2' + firstDeps + '$3')
        }
        file.write(text)
    }
}

publishing {
    publications {
        "rundeck"(MavenPublication) {
            artifactId = "rundeck"
            groupId = project.group
            artifact bootWar
            version = version
            pom {
                 name = "Rundeck library Rundeck"
                 description = project.description?:'Rundeck'
                 url = 'http://rundeck.org'
                 licenses {
                     license {
                         name = 'The Apache Software License, Version 2.0'
                         url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                         distribution = 'repo'
                     }
                 }
                 developers {
                     developer {
                         id = 'gschueler'
                         name = 'Greg Schueler'
                         email = 'greg@rundeck.com'
                     }
                 }
                 scm {
                     connection = 'scm:git:git@github.com/rundeck/rundeck.git'
                     developerConnection = 'scm:git:git@github.com:rundeck/rundeck.git'
                     url = 'https://github.com/rundeck/rundeck'
                 }
             }
        }
    }
    repositories {
         maven {
             // change URLs to point to your repos, e.g. http://my.org/repo
             def releasesRepoUrl = 'https://ossrh-staging-api.central.sonatype.com/service/local/'
             def snapshotsRepoUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
             url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
             credentials {
                 username findProperty('sonatypeUsername')
                 password findProperty('sonatypePassword')
             }
         }
     }
}

apply from: "${rootDir}/gradle/java.gradle"
apply from: "../gradle/java-version.gradle"


if (project.hasProperty('signing.keyId') && project.hasProperty('signing.password')) {
    signing {
        sign(publishing.publications)
    }
}

