plugins {
  id "com.moowork.node"
  id 'base'
}

configurations{
  lib
  packages
}

node{
    download = project.hasProperty('node.install')
    version = project.hasProperty('node.install') ? project.getProperty('node.install') : null
}

def runNpmBuild = tasks.register('runNpmBuild', NpmTask) {
    it.inputs.file 'package.json'
    it.inputs.file 'package-lock.json'
    it.inputs.file 'vue.config.js'
    it.inputs.file 'tsconfig.json'

    it.inputs.dir 'src'

    it.outputs.dir(file("$buildDir"))
    it.outputs.cacheIf { true }

    def npmCommand = System.env.CI ?
      'ci:build' :
      'dev:build'

    args = ['run', npmCommand]
}


def runNpmPack = tasks.register('runNpmPack', NpmTask) {
    it.group 'build'
    it.dependsOn runNpmBuild

    it.inputs.dir 'lib'
    it.inputs.dir 'theme'
    it.inputs.dir 'theme-next'
    it.inputs.dir '.storybook'

    it.outputs.dir("build")
    it.outputs.cacheIf { true }

    args = ['pack']

    it.doLast {
        copy {
            from './'
            into 'build'
            include '*.tgz'
        }
    }
}

def runNpmVersion = tasks.register('runNpmVersion', NpmTask) {
    args = [ 'version' ]

    if (project.environment == 'release') {
        if (project.vTag == 'GA')
            args << "${project.vNum}".toString()
        else
            args << "${project.vNum}-${project.vTag}.${new Date().getTime()}".toString()
    }
    else {
        args << "${project.vNum}-snapshot.${new Date().getTime()}".toString()
    }
}

tasks.register('runNpmPublish', NpmTask) {
    dependsOn runNpmBuild
    dependsOn runNpmVersion

    args = ['publish', '--dry-run']

    if (project.dryRun == false)
        args = args - '--dry-run'

    if (project.environment != 'release') {
        args += ['--tag', 'snapshot']
    }
}

assemble.dependsOn runNpmBuild

artifacts {
  lib(file: file(buildDir), name: "${project.name}", type: 'directory', builtBy: runNpmBuild)
  packages(file: file("$projectDir/build"), builtBy: runNpmPack)
}
