plugins {
  id "com.moowork.node" version "1.2.0"
  id 'base'
}

ext.spaBuildDir = "${buildDir}/spa"

configurations{
  spa
}

node{
    download = project.hasProperty('node.install')
    version = project.hasProperty('node.install') ? project.getProperty('node.install') : null
}

task npmCI(type: NpmTask, group: 'Node') {
  dependsOn 'npmCISwitch'
  enabled false
  args = ['ci']

  inputs.file(file("${projectDir}/package.json"))
  inputs.file(file("${projectDir}/package-lock.json"))

  outputs.dir(file("${projectDir}/node_modules"))
}

task npmCISwitch {
  inputs.file(file("${projectDir}/package.json"))
  inputs.file(file("${projectDir}/package-lock.json"))
  inputs.dir "src"
  inputs.dir "build"
  inputs.dir "config"

  outputs.file "${buildDir}/switch"

  outputs.cacheIf {true}
  doLast {
    tasks.npmCI.enabled = true
  }
}

task runNpmBuild(type: NpmTask, group: 'build') {
    dependsOn 'npmCISwitch', 'npmCI'

    inputs.file 'package.json'
    inputs.file 'package-lock.json'
    inputs.dir "src"
    inputs.dir "build"
    inputs.dir "config"

    outputs.dir(file("$spaBuildDir"))
    outputs.cacheIf {true}

    args = ['run', 'build']
}

assemble.dependsOn runNpmBuild

artifacts {
  spa(file: file(spaBuildDir), name: "${project.name}", type: 'directory', builtBy: runNpmBuild)
}

