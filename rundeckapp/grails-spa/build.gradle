plugins {
  id "com.github.node-gradle.node"
  id 'base'
}

ext.spaBuildDir = "${buildDir}/spa"

configurations{
  spa
  lib
  uiTrellis {
      canBeConsumed(true)
  }
}

dependencies {
    lib project(path: ':rundeckapp:grails-spa:packages:ui-trellis', configuration: 'lib')
    uiTrellis project(path: ':rundeckapp:grails-spa:packages:ui-trellis', configuration: 'packages')
}

node{
    // Whether to download and install a specific Node.js version or not
    // If false, it will use the globally installed Node.js
    // If true, it will download node using above parameters
    // Note that npm is bundled with Node.js
    download = false
    
    // Version of node to download and install (only used if download is true)
    // It will be unpacked in the workDir
    version = "16.18.0"
}


task noop() {
  doLast {
    true
  }
}

tasks.register("runNpmBuild", NpmTask) {
    // dependsOn configurations.uiTrellis

    // inputs.files(configurations.uiTrellis.allArtifacts)
    // inputs.file 'packages/ui-trellis/build/package/rundeck-ui-trellis.tgz'
    inputs.file 'packages/ui-trellis/package.json'
    inputs.file 'packages/ui-trellis/package-lock.json'
    inputs.file 'packages/ui-trellis/babel.config.js'
    inputs.file 'packages/ui-trellis/vue.config.app.js'
    inputs.file 'packages/ui-trellis/tsconfig.json'
    inputs.file 'packages/ui-trellis/.env.development'
    inputs.file 'packages/ui-trellis/.env.production'

    inputs.dir 'packages/ui-trellis/src/app'

    outputs.dir(file("$spaBuildDir"))
    outputs.cacheIf { false }

    def npmCommand = System.env.CI ?
      'ci:app:build' :
      'dev:app:build'

    args = ['run', npmCommand]

    execOverrides {
      it.workingDir = './packages/ui-trellis/src/app'
    }
}

assemble.dependsOn runNpmBuild

artifacts {
  spa(file: file(spaBuildDir), name: "${project.name}", type: 'directory', builtBy: runNpmBuild)
}

